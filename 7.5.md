本节最后修改于 `2022 / 11 / 12`

# 搞个雪球菜单·续

一节写太多读起来不太舒服，就分了一节，我们接着来搞。

上一节是弄完了除了主体指令之外的所有东西，那么这一节我们就来弄主体指令。

## 用户MP事件监听器

我们知道一共有三个操作：扔雪球、抬头、低头。那不用多说先来三组监听器！

```
# 扔雪球监听器
[+,X,-,4] execute @e[type=snowball] ~~~ tag @p[r=0.2] add SB_sb-ing
[+,L,-,0] kill @e[type=snowball]
[+,L,-,0] # 这里是扔雪球MP过程内的指令
[+,L,-,0] tag @a[tag=SB_sb-ing] remove SB_sb-ing

# 抬头监听器
[+,X,-,4] tag @a[rx=-89] add SB_hdUp-ing
[+,L,-,0] # 这里是抬头MP过程内的指令
[+,L,-,0] tag @a[tag=SB_hdUp-ing] remove SB_hdUp-ing

# 低头监听器
[+,X,-,4] tag @a[rxm=89] add SB_hdDn-ing
[+,L,-,0] # 这里是低头MP过程内的指令
[+,L,-,0] tag @a[tag=SB_hdDn-ing] remove SB_hdDn-ing
```

监听器框架有了，然后我们可以开始一个一个往里填指令了。

首先是扔雪球监听器。这个雪球菜单的要求是扔一次菜单就出现，再仍一次切换选项。在最后一个选项切换的时候返回开头。

```
# 扔雪球MP过程内的指令
[+,L,-,0] scoreboard players add @a[tag=SB_sb-ing] SB_ui 1 //让扔雪球的人SB_ui加1，目的是切换选项
[+,L,-,0] scoreboard players set @a[tag=SB_sb-ing,scores={SB_ui=3}] SB_ui 0 //让扔雪球且SB_ui=3的人的SB_ui变成0，目的是让主菜单选完的人选回第一项
[+,L,-,0] scoreboard players set @a[tag=SB_sb-ing,scores={SB_ui=13}] SB_ui 10 //让扔雪球且SB_ui=13的人的SB_ui变成10，目的是让夸赞玉米菜单选完的人选回第一项
```

理论上我们的扔雪球监听器就这么简单的写完了。

扔雪球后面的MP事件是抬头。我们知道在不同时候抬头会有不同的效果，例如在选中索要雪球选项时抬头会调用`索要雪球模块`、在选中夸赞玉米菜单时抬头会进入夸赞玉米菜单等等。

```
# 抬头MP过程内的指令
[+,L,-,0] tag @a[tag=SB_hdUp-ing,scores={SB_ui=0}] add CM_do //调用切换模式模块
[+,L,-,0] tag @a[tag=SB_hdUp-ing,scores={SB_ui=1}] add GS_do //调用索要雪球模块
[+,L,-,0] scoreboard players set @a[tag=SB_hdUp-ing,scores={SB_ui=2}] SB_ui 10 //进入夸赞玉米菜单
[+,L,-,0] execute @a[tag=SB_hdUp-ing,scores={SB_ui=10..19}] ~~~ scoreboard players operation @s HS_num = @s SB_ui //调用夸赞玉米模块
[+,L,-,0] scoreboard players remove @a[scores={HS_num=10..19}] HS_num 9 //继续调用夸赞玉米模块
```

最后还差一个低头的监听器。低头主要就是取消或者返回上一页菜单，那就比较简单了。

```
# 低头MP过程内的指令
[+,L,-,0] scoreboard players set @a[tag=SB_hdDn-ing,scores={SB_ui=0..9}] SB_ui -2 //调用雪球菜单提示模块，提示“取消”
[+,L,-,0] scoreboard players set @a[tag=SB_hdDn-ing,scores={SB_ui=10..19}] SB_ui 0 //回到上一级菜单
```

## 提示MP模块

我们从上面写到下面，发现好像只需要两种提示，一种是成功提示，一种是取消提示。

这倒是好事，至少写的指令挺少的。

```
# 雪球菜单提示模块 入口计分板：“SB_ui”
[+,X,-,4] title @a[scores={SB_ui=-1}] actionbar 已成功 //提示“成功”
[+,L,-,0] title @a[scores={SB_ui=-2}] actionbar 已取消 //提示“取消”
[+,L,-,0] scoreboard players reset @a[scores={SB_ui=-10..-1}] SB_ui //重设计分板
```


## 界面MP模块

于是到了现在，万事俱备只欠东风。我们已经把所有逻辑都写完了，就差提供一个界面了。

由于我们的逻辑和界面是分开的几个MP模块，所以我们只需要给`SB_ui`不同的人显示不同的界面就可以，不需要再关心逻辑。

```
# 雪球菜单界面模块 入口计分板：“SB_ui”
[+,X,-,4] title @a[scores={SB_ui=0}] actionbar |<切换创造/生存>| 索要雪球 | 夸赞玉米 |
[+,L,-,0] title @a[scores={SB_ui=1}] actionbar | 切换创造/生存 |<索要雪球>| 夸赞玉米 |
[+,L,-,0] title @a[scores={SB_ui=2}] actionbar | 切换创造/生存 | 索要雪球 |<夸赞玉米>|
[+,L,-,0] title @a[scores={SB_ui=10}] actionbar |<玉米好帅>| 玉米真帅 | 玉米很帅 |
[+,L,-,0] title @a[scores={SB_ui=11}] actionbar | 玉米好帅 |<玉米真帅>| 玉米很帅 |
[+,L,-,0] title @a[scores={SB_ui=12}] actionbar | 玉米好帅 | 玉米真帅 |<玉米很帅>|
```

## 全部指令

```
# 切换模式模块 入口标签：“CM_do”
[+,X,-,4] tag @a[tag=CM_do,m=0] add CM_m0
[+,L,-,0] gamemode 0 @a[tag=CM_do,m=1]
[+,L,-,0] gamemode 1 @a[tag=CM_do,tag=CM_m0]
[+,L,-,0] tag @a[tag=CM_m0] remove CM_m0
[+,L,-,0] scoreboard players set @a[tag=CM_do,scores={SB_ui=0..}] SB_ui -1
[+,L,-,0] tag @a[tag=CM_do] remove CM_do
```

```
# 索要雪球模块 入口标签：“GS_do”
[+,X,-,4] scoreboard players add @a[tag=GS_do] add GS_sbNum 1
[+,L,-,0] execute @a[tag=GS_do] ~~~ scoreboard players operation @s GS_sbNeed = @s GS_sbNum
[+,L,-,0] tag @a[tag=GS_do] remove GS_do

# 索要雪球模块 的 给雪球模块 入口计分板：“GS_sbNeed”
[+,X,-,0] give @a[scores={GS_sbNeed=1..}] snowball
[+,L,-,0] scoreboard players remove @a[scores={GS_sbNeed=1..}] GS_sbNeed 1
[+,L,-,0] scoreboard players set @a[scores={GS_sbNeed=0,SB_ui=0..}] SB_ui -1
[+,L,-,0] scoreboard players reset @a[scores={GS_sbNeed=0}] GS_sbNeed
```

```
# 夸赞玉米模块 入口计分板：“HS_num”
[+,X,-,4] execute @a[scores={HS_num=1}] ~~~ say 玉米好帅
[+,L,-,0] execute @a[scores={HS_num=2}] ~~~ say 玉米真帅
[+,L,-,0] execute @a[scores={HS_num=3}] ~~~ say 玉米很帅
[+,L,-,0] scoreboard players set @a[scores={HS_num=0..,SB_ui=0..}] SB_ui -1
[+,L,-,0] scoreboard players reset @a[scores={HS_num=0..}] HS_num
```

```
# 扔雪球监听器
[+,X,-,4] execute @e[type=snowball] ~~~ tag @p[r=0.2] add SB_sb-ing
[+,L,-,0] kill @e[type=snowball]
[+,L,-,0] scoreboard players add @a[tag=SB_sb-ing] SB_ui 1
[+,L,-,0] scoreboard players set @a[tag=SB_sb-ing,scores={SB_ui=3}] SB_ui 0
[+,L,-,0] scoreboard players set @a[tag=SB_sb-ing,scores={SB_ui=13}] SB_ui 10
[+,L,-,0] tag @a[tag=SB_sb-ing] remove SB_sb-ing

# 抬头监听器
[+,X,-,4] tag @a[rx=-89] add SB_hdUp-ing
[+,L,-,0] tag @a[tag=SB_hdUp-ing,scores={SB_ui=0}] add CM_do
[+,L,-,0] tag @a[tag=SB_hdUp-ing,scores={SB_ui=1}] add GS_do
[+,L,-,0] scoreboard players set @a[tag=SB_hdUp-ing,scores={SB_ui=2}] SB_ui 10
[+,L,-,0] execute @a[tag=SB_hdUp-ing,scores={SB_ui=10..19}] ~~~ scoreboard players operation @s HS_num = @s SB_ui
[+,L,-,0] scoreboard players remove @a[scores={HS_num=10..19}] HS_num 9
[+,L,-,0] tag @a[tag=SB_hdUp-ing] remove SB_hdUp-ing

# 低头监听器
[+,X,-,4] tag @a[rxm=89] add SB_hdDn-ing
[+,L,-,0] scoreboard players set @a[tag=SB_hdDn-ing,scores={SB_ui=0..9}] SB_ui -2
[+,L,-,0] scoreboard players set @a[tag=SB_hdDn-ing,scores={SB_ui=10..19}] SB_ui 0
[+,L,-,0] tag @a[tag=SB_hdDn-ing] remove SB_hdDn-ing
```

```
# 雪球菜单提示模块 入口计分板：“SB_ui”
[+,X,-,4] title @a[scores={SB_ui=-1}] actionbar 已成功
[+,L,-,0] title @a[scores={SB_ui=-2}] actionbar 已取消
[+,L,-,0] scoreboard players reset @a[scores={SB_ui=-10..-1}] SB_ui
```

```
# 雪球菜单界面模块 入口计分板：“SB_ui”
[+,X,-,4] title @a[scores={SB_ui=0}] actionbar |<切换创造/生存>| 索要雪球 | 夸赞玉米菜单 |
[+,L,-,0] title @a[scores={SB_ui=1}] actionbar | 切换创造/生存 |<索要雪球>| 夸赞玉米菜单 |
[+,L,-,0] title @a[scores={SB_ui=2}] actionbar | 切换创造/生存 | 索要雪球 |<夸赞玉米菜单>|
[+,L,-,0] title @a[scores={SB_ui=10}] actionbar |<玉米好帅>| 玉米真帅 | 玉米很帅 |
[+,L,-,0] title @a[scores={SB_ui=11}] actionbar | 玉米好帅 |<玉米真帅>| 玉米很帅 |
[+,L,-,0] title @a[scores={SB_ui=12}] actionbar | 玉米好帅 | 玉米真帅 |<玉米很帅>|
```

虽然我不是特别了解雪球菜单，不知道这个命令量算多还是算少。但是可以知道的一点是，这个雪球菜单对维护和拓展开发非常友好。这是设计模式的价值。

这就写完了？其实不是。如果你搞出来了，你会发现在选择夸赞玉米菜单试着抬头后，还没等你反应过来，就已把玉米夸赞了！

究其原因是没有菜单之间的缓冲。解决方法是让一个人抬头后两秒之内不再计算抬头事件。如果读者有兴趣的话可以尝试自己编写出来，碍于篇幅这里就不详述了。